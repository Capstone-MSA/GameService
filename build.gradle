plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.openapi.generator' version '6.5.0'
}

group = 'capstone.bownling'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.1"
    }
}

dependencies {
    // Spring Boot 기본
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Feign Client
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // Swagger/OpenAPI UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    // OpenAPI generator 사용 결과 모델 처리용
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    // Jakarta API
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'

    // 기타
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.webjars:stomp-websocket:2.3.4'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'

    implementation 'org.springframework.boot:spring-boot-starter-security'

}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    verbose.set(true)
    generatorName.set("spring")
    library.set("spring-boot")
    inputSpec.set(project.file("$rootDir/src/main/resources/apispec/game-api.yaml").absolutePath)
    outputDir.set(project.file("$buildDir/generated-sources").absolutePath)
    apiPackage.set("capstone.bowling.openapi.rest")
    modelPackage.set("capstone.bowling.openapi.domain")
    configOptions.set([
            interfaceOnly: "true",
            useBeanValidation: "true",
            performBeanValidation: "true",
            serializableModel: "true",
            useAllOfForInheritance: "true", // 선택 사항
            withXml: "false",               // 선택 사항
            modelMutable: "true",           // 중요
            generateConstructor: "true" ,    // ✅ 모든 필드를 받는 생성자 생성

            useJakartaEe: "true" // ✅ javax → jakarta 전환 옵션
    ])
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-sources/src/main/java']
        }
    }
}
